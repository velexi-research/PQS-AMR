c***********************************************************************
c
c interface_kernels_2d.f
c
c Subroutines to initialize fluid-fluid interface for example
c application with no solid phase in two spatial dimensions.
c
c***********************************************************************

c***********************************************************************
c
c Initalize level set function, phi, for fluid-fluid interface to be
c a circle.
c
c Parameters
c ----------
c phi: [output] level set function that defines fluid-fluid interface
c
c x_lower: array containing coordinates of lower corner of grid
c    (excluding ghost cells)
c
c dx: array containing grid spacing in each coordinate direction
c
c center: array containing coordinates of center of circle
c
c radius: radius of circle
c
c *_gb_lo: lower corner of index range for ghostbox
c
c *_gb_hi: upper corner of index range for ghostbox
c
c fb_lo: lower corner of index range for fillbox
c
c fb_hi: upper corner of index range for fillbox
c
c***********************************************************************
      subroutine initCircle(
     &  phi,
     &  phi_gb_lo, phi_gb_hi,
     &  fb_lo, fb_hi,
     &  x_lower,
     &  dx,
     &  center,
     &  radius)
c***********************************************************************
      implicit none

      integer phi_gb_lo(1:2), phi_gb_hi(1:2)
      @REAL@ phi(phi_gb_lo(1):phi_gb_hi(1),
     &           phi_gb_lo(2):phi_gb_hi(2))

      integer fb_lo(1:2), fb_hi(1:2)

      real*8 dx(1:2)
      real*8 x_lower(1:2)

      real*8 center(1:2)
      real*8 radius

      real*8 x, y
      integer i, j

c     loop over grid
      do j=fb_lo(2), fb_hi(2)
        do i=fb_lo(1), fb_hi(1)

          x = x_lower(1) + dx(1)*(i-fb_lo(1)+0.5) - center(1)
          y = x_lower(2) + dx(2)*(j-fb_lo(2)+0.5) - center(2)

          phi(i,j) = sqrt(x**2 + y**2) - radius

        enddo
      enddo
c     } end loop over grid

      return
      end
c***********************************************************************

c***********************************************************************
c
c Initalize level set function, phi, for fluid-fluid interface to be
c a starfish shape.
c
c Parameters
c ----------
c phi: [output] level set function that defines fluid-fluid interface
c
c x_lower: array containing coordinates of lower corner of grid
c    (excluding ghost cells)
c
c dx: array containing grid spacing in each coordinate direction
c
c center: array containing coordinates of center of circle
c
c radius: radius of circle
c
c *_gb_lo: lower corner of index range for ghostbox
c
c *_gb_hi: upper corner of index range for ghostbox
c
c fb_lo: lower corner of index range for fillbox
c
c fb_hi: upper corner of index range for fillbox
c
c***********************************************************************
      subroutine initStarfish(
     &  phi,
     &  phi_gb_lo, phi_gb_hi,
     &  fb_lo, fb_hi,
     &  x_lower,
     &  dx,
     &  center,
     &  radius)
c***********************************************************************
      implicit none

      integer phi_gb_lo(1:2), phi_gb_hi(1:2)
      @REAL@ phi(phi_gb_lo(1):phi_gb_hi(1),
     &           phi_gb_lo(2):phi_gb_hi(2))

      integer fb_lo(1:2), fb_hi(1:2)

      real*8 dx(1:2)
      real*8 x_lower(1:2)

      real*8 center(1:2)
      real*8 radius

      real*8 x, y
      real*8 theta
      integer i, j

c     loop over grid
      do j=fb_lo(2), fb_hi(2)
        do i=fb_lo(1), fb_hi(1)

          x = x_lower(1) + dx(1)*(i-fb_lo(1)+0.5) - center(1)
          y = x_lower(2) + dx(2)*(j-fb_lo(2)+0.5) - center(2)

          theta = atan2(y,x)
          phi(i,j) = sqrt(x**2+y**2) - radius*(1+0.5*sin(5*theta));

        enddo
      enddo
c     } end loop over grid

      return
      end
c***********************************************************************
